{
  elementTypeClass = "com.alecstrong.sqlite.psi.core.SqliteElementType"

  // "Real" rules are implemented by SqliteParser, so it acts as the parser util class.
  overrides="com.alecstrong.sqlite.psi.core.SqliteParser"

  parserImports=[
    // If you want to use a token from the core grammar it must be statically imported.
    "static com.alecstrong.sqlite.psi.core.psi.SqliteTypes.STRING"
  ]
}
// The grammar still needs to be well formed and so should contain a root element that is not
// part of the generated psi.
root ::= column_def

// An overriden rule.
column_def ::= {column_name} sqlite_type [ 'AS' java_type ] ( {column_constraint} ) * {
  // In order for the core to function properly, when you override a rule it should still implement
  // the core interface for that rule.
  implements="com.alecstrong.sqlite.psi.core.psi.SqliteColumnDef"
  extends="com.alecstrong.sqlite.psi.core.psi.impl.SqliteColumnDefImpl"
  override=true
}

// Rules that your grammar uses.
sqlite_type ::= ( 'TEXT' | 'BLOB' | 'INTEGER' | 'REAL' )
java_type ::= string