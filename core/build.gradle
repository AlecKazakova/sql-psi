plugins {
  alias(libs.plugins.kotlinJvm)
  alias(libs.plugins.grammarKitComposer)
}

apply from: "$rootDir/gradle/gradle-mvn-push.gradle"

grammarKit {
  intellijRelease.set(libs.versions.ideaVersion)
}

sourceSets {
  main.java.srcDir "gen"
}

configurations {
  grammar {
    canBeResolved = true
    canBeConsumed = false
  }
}

dependencies {
  grammar libs.coroutines

  compileOnly libs.bundles.intelliJ

  testImplementation libs.junit
  testImplementation libs.truth
  testImplementation project(":test-fixtures")
}

generateLexer {
  source.set('src/main/kotlin/com/alecstrong/sql/psi/core/SqlLexer.flex')
  targetDir.set('gen/com/alecstrong/sql/psi/core/lexer/')
  targetClass.set('SqlLexer')
  purgeOldFiles.set(true)
}

// https://youtrack.jetbrains.com/issue/IDEA-301677
generatecom_alecstrong_sql_psi_core_sqlParser {
  classpath = (classpath + configurations.grammar)
}

compileKotlin.dependsOn generateLexer
