plugins {
  alias(libs.plugins.kotlinJvm)
  alias(libs.plugins.grammarKitComposer)
}

apply from: "$rootDir/gradle/gradle-mvn-push.gradle"

grammarKit {
  intellijRelease.set(libs.versions.ideaVersion)
}

sourceSets {
  main.java.srcDir "gen"
  main.java.srcDirs("$buildDir/generated/lexer/main/java", "$buildDir/generated/parser/main/java")
}

dependencies {
  compileOnly libs.bundles.intelliJ

  testImplementation libs.junit
  testImplementation libs.truth
  testImplementation project(":test-fixtures")
}

generateLexer {
  source.set('src/main/kotlin/com/alecstrong/sql/psi/core/SqlLexer.flex')
  targetDir.set('gen/com/alecstrong/sql/psi/core/lexer/')
  targetClass.set('SqlLexer')
  purgeOldFiles.set(true)
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
  dependsOn(generatecom_alecstrong_sql_psi_core_sqlParser, createComposablecom_alecstrong_sql_psi_core_sqlGrammar, generateLexer)
}

tasks.named("kotlinSourcesJar") {
  dependsOn(generatecom_alecstrong_sql_psi_core_sqlParser, createComposablecom_alecstrong_sql_psi_core_sqlGrammar, generateLexer)
}
