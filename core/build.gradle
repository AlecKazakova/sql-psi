apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.grammarkit'
apply plugin: 'com.alecstrong.grammar.kit.composer'
apply plugin: 'maven-publish'

import org.jetbrains.grammarkit.tasks.GenerateLexer

sourceSets {
  main.java.srcDir "gen"
}

dependencies {
  compileOnly dep.intellijCore

  implementation dep.kotlinStdLib
  implementation dep.collections

  testImplementation dep.intellijCore
  testImplementation dep.junit
  testImplementation dep.truth
}

task generateSqlLexer(type: GenerateLexer) {
  source 'src/main/kotlin/com/alecstrong/sql/psi/core/SqlLexer.flex'
  targetDir 'gen/com/alecstrong/sql/psi/core/lexer/'
  targetClass 'SqlLexer'
  purgeOldFiles = true
}

compileKotlin.dependsOn generateSqlLexer

apply from: "$rootDir/gradle/gradle-mvn-push.gradle"

publishing {
  repositories {
    maven {
      name = "GithubPackages"
      url = "https://maven.pkg.github.com/alecstrong/sql-psi"
      credentials {
        username = "alecstrong"
        password = System.getenv('GITHUB_TOKEN')
      }
    }
  }

  publications {
    sqlPsi(MavenPublication) {
      from(components.kotlin)
    }
  }
}
