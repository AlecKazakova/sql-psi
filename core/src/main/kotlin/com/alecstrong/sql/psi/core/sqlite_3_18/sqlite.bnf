{
  // Specify the parent parser.
  overrides="com.alecstrong.sql.psi.core.SqlParser"
  elementTypeClass = "com.alecstrong.sql.psi.core.SqlElementType"

  implements="com.alecstrong.sql.psi.core.psi.SqlCompositeElement"
  extends="com.alecstrong.sql.psi.core.psi.SqlCompositeElementImpl"

  parserImports=[
    "static com.alecstrong.sql.psi.core.psi.SqlTypes.DIGIT"
    "static com.alecstrong.sql.psi.core.psi.SqlTypes.ALTER"
    "static com.alecstrong.sql.psi.core.psi.SqlTypes.TABLE"
  ]
}
overrides ::= type_name | column_def | bind_parameter | alter_table_stmt

type_name ::= 'TEXT' | 'BLOB' | 'INTEGER' | 'REAL' {
  extends = "com.alecstrong.sql.psi.core.psi.impl.SqlTypeNameImpl"
  implements = "com.alecstrong.sql.psi.core.psi.SqlTypeName"
  override = true
}
column_def ::= [ javadoc ] {column_name} {type_name} ( {column_constraint} ) * {
  mixin = "com.alecstrong.sql.psi.core.sqlite_3_18.psi.mixins.ColumnDefMixin"
  implements = "com.alecstrong.sql.psi.core.psi.SqlColumnDef"
  override = true
}
bind_parameter ::= ( '?' [digit] | ':' {identifier} ) {
  extends = "com.alecstrong.sql.psi.core.psi.impl.SqlBindParameterImpl"
  implements = "com.alecstrong.sql.psi.core.psi.SqlBindParameter"
  override = true
}
alter_table_stmt ::= ALTER TABLE [ {database_name} '.' ] {table_name} {alter_table_rules} {
  extends = "com.alecstrong.sql.psi.core.psi.impl.SqlAlterTableStmtImpl"
  implements = "com.alecstrong.sql.psi.core.psi.SqlAlterTableStmt"
  pin = 1
  override = true
}