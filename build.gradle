import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
  id("org.jetbrains.kotlin.jvm") version "1.6.21" apply false
  id("com.alecstrong.grammar.kit.composer") version "0.1.10" apply false
  id("com.diffplug.spotless") version "5.11.1"
  id("com.vanniktech.maven.publish") version "0.19.0"
  id("org.jetbrains.dokka") version "1.6.10"
}

spotless {
  kotlin {
    target "**/*.kt"
    targetExclude "**/gen/**/*.*"
    ktlint('0.41.0').userData(["indent_size": "2"])
    trimTrailingWhitespace()
    endWithNewline()
  }
}

allprojects {
  repositories {
    mavenCentral()
    maven { url 'https://www.jetbrains.com/intellij-repository/releases' }
    maven { url "https://cache-redirector.jetbrains.com/intellij-dependencies" }
    maven { url "https://cache-redirector.jetbrains.com/maven.pkg.jetbrains.space/kotlin/p/kotlin/kotlin-ide-plugin-dependencies" }
  }

  group = GROUP
  version = VERSION_NAME

  tasks.withType(KotlinCompile) {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    kotlinOptions {
      jvmTarget = "11"
      apiVersion = "1.6"
    }
  }

  tasks.withType(Test) {
    testLogging {
      events = ["failed", "skipped", "passed"]
      exceptionFormat "full"
    }
  }

  configurations.all {
    exclude group: 'com.jetbrains.rd'
    exclude group: 'com.github.jetbrains', module: 'jetCheck'
    exclude group: 'org.roaringbitmap'
    exclude group: 'ai.grazie.nlp'
    exclude group: 'ai.grazie.spell'
    exclude group: 'ai.grazie.utils'
    exclude group: 'io.ktor'
    exclude group: 'com.jetbrains.infra'
    exclude group: 'org.jetbrains.teamcity'
  }
}
