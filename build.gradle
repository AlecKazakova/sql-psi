import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
  alias(libs.plugins.kotlinJvm) apply false
  alias(libs.plugins.spotless)
  alias(libs.plugins.mavenPublish)
  alias(libs.plugins.dokka)
}

spotless {
  kotlin {
    target "**/*.kt"
    targetExclude "**/gen/**/*.*"
    ktlint('0.46.1').editorConfigOverride([
      "indent_size": "2",
       "disabled_rules": "package-name",
       "ij_kotlin_allow_trailing_comma": "true",
       "ij_kotlin_allow_trailing_comma_on_call_site": "true",
    ])
    trimTrailingWhitespace()
    endWithNewline()
  }
}

allprojects {
  repositories {
    mavenCentral()
    maven { url 'https://www.jetbrains.com/intellij-repository/releases' }
    maven { url "https://cache-redirector.jetbrains.com/intellij-dependencies" }
    maven { url "https://cache-redirector.jetbrains.com/maven.pkg.jetbrains.space/kotlin/p/kotlin/kotlin-ide-plugin-dependencies" }
  }

  group = GROUP
  version = VERSION_NAME

  tasks.withType(KotlinCompile) {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    kotlinOptions {
      jvmTarget = "11"
      apiVersion = "1.3"
    }
  }

  tasks.withType(Test) {
    testLogging {
      events = ["failed", "skipped", "passed"]
      exceptionFormat "full"
    }
  }

  configurations.all {
    exclude group: 'com.jetbrains.rd'
  }
}

task printVersion {
  doLast {
    // to get the current version to inject it into sqldelight
    file("version").text = project.version
  }
}
